tables = [
  # title, included modes, hideInPrint
  [gettext('Compensable Items'), {SO: 0}, false]
  [gettext('Returnable Items'),  {BR: 0, ST: 0}, false]
  [gettext('Other Items'),       {MI: 0, RE: 0, CO: 0}, false]
  [gettext('Not brought to event'), {AD: 0}, true]
]

class @VendorReport extends CheckoutMode
  constructor: (cfg, switcher, vendor) ->
    super(cfg, switcher)
    @vendor = vendor

  title: -> gettext("Item Report")
  inputPlaceholder: -> "Search"

  actions: -> [
    ["", (query) => @switcher.switchTo(VendorFindMode, query)]
    [@cfg.settings.logoutPrefix,      @onLogout]
  ]

  enter: ->
    super
    @cfg.uiRef.body.append(new VendorInfo(@vendor).render())
    compensateButton = $('<input type="button">')
      .addClass('btn btn-primary')
      .attr('value', gettext('Compensate'))
      .click(@onCompensate)
    checkoutButton = $('<input type="button">')
      .addClass('btn btn-primary')
      .attr('value', gettext('Return Items'))
      .click(@onReturn)
    @cfg.uiRef.body.append(
      $('<form class="hidden-print">').append(
        compensateButton,
        checkoutButton,
      )
    )

    Api.item_list(
      vendor: @vendor.id
    ).done(@onGotItems)

  onGotItems: (items) =>
    for [name, states, hidePrint] in tables
      matchingItems = (i for i in items when states[i.state]?)
      table = new ItemReportTable(name)
      table.update(matchingItems)
      rendered_table = table.render()
      if hidePrint then rendered_table.addClass('hidden-print')
      @cfg.uiRef.body.append(rendered_table)

  onCompensate: => @switcher.switchTo(VendorCompensation, @vendor)
  onReturn: =>     @switcher.switchTo(VendorCheckoutMode, @vendor)
